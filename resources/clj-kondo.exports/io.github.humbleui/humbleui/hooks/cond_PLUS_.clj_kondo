(ns ^:no-doc hooks.cond+
  "A clj-kondo hook to allow linting of humbleui `cond+` macro."
  (:require
    [clj-kondo.hooks-api :as api]))

(defn cond+-hook [form]
  (let [[_ test expr & rest] (:children (:node form))
        tail (when rest
               (api/list-node
                 (list*
                   (api/token-node 'io.github.humbleui.core/cond+)
                   rest)))]
    {:node
     (cond
       (and (api/keyword-node? test) (= :do (:k test)))
       (api/list-node
         (list
           (api/token-node 'do)
           expr
           tail))
               
       (and (api/keyword-node? test) (= :let (:k test)))
       (api/list-node
         (list
           (api/token-node 'let)
           expr
           tail))
                 
       (and (api/keyword-node? test) (= :some (:k test)))
       (api/list-node
         (list
           (api/token-node 'or)
           expr
           tail))
                   
       tail
       (api/list-node
         (list
           (api/token-node 'if)
           test
           expr
           tail))
             
       :else
       (api/list-node
         (list
           (api/token-node 'when)
           test
           expr)))}))
